load("@rules_cc//cc:cc_test.bzl", "cc_test")
load("//autoconf:autoconf.bzl", "autoconf")
load("//autoconf:autoconf_hdr.bzl", "autoconf_hdr")
load("//autoconf:macros.bzl", "macros")
load("//autoconf:package_info.bzl", "package_info")
load("//autoconf/private/tests:diff_test.bzl", "diff_test")

package_info(
    name = "package",
    package_name = "test_compiler_capabilities",
    package_version = "1.0.0",
)

autoconf(
    name = "autoconf",
    checks = [
        # Test C language keywords (still useful)
        macros.AC_C_RESTRICT(),
        macros.AC_C_INLINE(),
    ] + select({
        # Test compiler flag support
        "@rules_cc//cc/compiler:msvc-cl": [
            macros.AC_CHECK_C_COMPILER_FLAG("/Wall"),
            macros.AC_CHECK_C_COMPILER_FLAG("/std:c99"),
            macros.AC_CHECK_CXX_COMPILER_FLAG("/std:c++17"),
        ],
        "//conditions:default": [
            macros.AC_CHECK_C_COMPILER_FLAG("-Wall"),
            macros.AC_CHECK_C_COMPILER_FLAG("-std=c99"),
            macros.AC_CHECK_CXX_COMPILER_FLAG("-std=c++17"),
        ],
    }) + [

        # Test compiler capability
        macros.AC_PROG_CC_C_O(),
    ],
    deps = [":package"],
)

autoconf_hdr(
    name = "config",
    out = "config.h",
    template = "config.h.in",
    deps = [":autoconf"],
)

diff_test(
    name = "diff_test_unix",
    file1 = "golden_config_unix.h.in",
    file2 = ":config.h",
    target_compatible_with = select({
        "@platforms//os:windows": ["@platforms//:incompatible"],
        "//conditions:default": [],
    }),
)

diff_test(
    name = "diff_test_windows",
    file1 = "golden_config_windows.h.in",
    file2 = ":config.h",
    target_compatible_with = select({
        "@platforms//os:windows": [],
        "//conditions:default": ["@platforms//:incompatible"],
    }),
)

cc_test(
    name = "test_compiler_capabilities",
    srcs = [
        "test_compiler_capabilities.c",
        ":config.h",
    ],
)
